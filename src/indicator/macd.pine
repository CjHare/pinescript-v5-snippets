// MACD indicator
//
// Input configuration
// - source: close; the OHLC input for the fast and slow moving averages
// - fastLength: 12; number of periods for the source fast moving average 
// - slowLength: 26; number of periods for the source slow moving average
// - signalSmoothing: 9; number of periods for the macd moving average
//
// Output:
// - Macd solid line
// - Signal solid line
// - Histogram of difference between the MACD and Signal lines, positive: green, negative: red 
// - Zero dotted line

//@version=5
indicator("MACD", overlay=false, timeframe="D", timeframe_gaps=true)

// Input parameters
source = input.source(close, title="Source")
fastLength = input.int(12, minval=1, title="Fast Length")
slowLength = input.int(26, minval=1, title="Slow Length")
signalSmoothing = input.int(9, minval=1, title="Signal Smoothing")

// MACD calculation
macd() =>
    fastMA = ta.ema(source, fastLength)
    slowMA = ta.ema(source, slowLength)
    macdLine = fastMA - slowMA
    signalLine = ta.ema(macdLine, signalSmoothing)
    [macdLine, signalLine]

[macdLine, signalLine] = macd()
histogram = macdLine - signalLine

// Plot the lines
plot(macdLine, color=color.blue, title="MACD Line")
plot(signalLine, color=color.orange, title="Signal Line")

// Historgram colored by sign
histogramColor = color.green
if(histogram < 0)
    histogramColor := color.red

plot(histogram, color=histogramColor, title="Histogram", style=plot.style_columns, linewidth=2)

hline(0, "Zero Line", color=color.gray)